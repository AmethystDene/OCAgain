buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'scala'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = config.mod.version
group = config.mod.group
archivesBaseName = config.mod.name

def getGitRef() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Throwable e) {
        return "unknown"
    }
}

if (System.getenv("PROMOTED_NUMBER") != null)
    version += ".${System.getenv("PROMOTED_NUMBER")}"
else if (System.getenv("BUILD_NUMBER") != null)
    version += ".${System.getenv("BUILD_NUMBER")}"
else
    version += "+" + getGitRef()

ext.simpleVersion = version
version = "MC${config.minecraft.version}-${project.version}"

minecraft {
    mappings channel: "official", version: config.minecraft.version

    accessTransformer = file("src/main/resources/oc-at.cfg")

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            mods {
                opencomputers {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url "https://maven.cil.li/" }
    maven { url "https://minecraft.curseforge.com/api/maven/" }
}

configurations {
    embedded
    implementation.extendsFrom embedded
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.minecraft.version}-${config.forge.version}"

    compileOnly fg.deobf("li.cil.tis3d:TIS-3D:MC1.12-${config.tis3d.version}") {
        exclude module: "jei_1.12"
    }
    compileOnly fg.deobf("com.mod-buildcraft:buildcraft-api:${config.buildcraft.version}")
    compileOnly fg.deobf("MCMultiPart2:MCMultiPart-exp:${config.mcmp.version}")
    compileOnly ("net.sengir.forestry:forestry_1.12.2:${config.forestry.version}") {
        exclude module: "jei_1.12"
    }
    compileOnly fg.deobf("net.industrial-craft:industrialcraft-2:${config.ic2.version}")
    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${config.hwyla.version}:api")
    compileOnly fg.deobf("dan200.computercraft:ComputerCraft:${config.cc.version}")
    compileOnly fg.deobf("charset:Charset:${config.charset.version}:api")

    compileOnly ("appeng:appliedenergistics2:${config.ae2.version}:api") {
        transitive = false
    }
    compileOnly "extracells2:ExtraCells-api:${config.extracells.version}"

    compileOnly("mekanism:Mekanism:${config.mekanism.version}:api") {
        transitive = false
    }

    compileOnly ("codechicken:ForgeMultipart:${config.minecraft.version}-${config.forgemultipart.version}:universal") {
        exclude module: "jei_1.12"
        exclude module: "CodeChickenLib"
    }

    compileOnly ("codechicken:ChickenASM:${config.casm.version}")

    compileOnly "mezz.jei:jei_${config.minecraft.version}:${config.jei.version}"
    compileOnly "codechicken:CodeChickenLib:${config.minecraft.version}-${config.ccl.version}:universal"
    compileOnly "codechicken:WR-CBE:${config.minecraft.version}-${config.wrcbe.version}:universal"

    compileOnly ("mrtjp:ProjectRed:${config.projred.version}:Base") {
        exclude module: "NotEnoughItems"
        exclude module: "CodeChickenLib"
        exclude module: "jei_1.12"
        exclude module: "ForgeMultipart"
    }

    compileOnly ("mrtjp:ProjectRed:${config.projred.version}:integration") {
        exclude module: "NotEnoughItems"
        exclude module: "CodeChickenLib"
        exclude module: "jei_1.12"
        exclude module: "ForgeMultipart"
    }

    compileOnly ("mrtjp:MrTJPCore:${config.mrtjpcore.version}:universal") {
        exclude module: "NotEnoughItems"
        exclude module: "CodeChickenLib"
        exclude module: "jei_1.12"
        exclude module: "ForgeMultipart"
    }

    embedded files('libs/OpenComputers-JNLua.jar', 'libs/OpenComputers-LuaJ.jar')

    compileOnly "codechicken:EnderStorage:${config.minecraft.version}-${config.enderstorage.version}:universal"
}

processResources {
    inputs.property "version", project.simpleVersion
    inputs.property "mcversion", config.minecraft.version
    inputs.property "fversion", config.forge.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.simpleVersion, 'mcversion':config.minecraft.version, 'fversion':config.forge.version
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'application.conf'
        filter { line ->
            line.replaceAll("@VERSION@", project.simpleVersion)
        }
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'application.conf'
    }

    // Move access transformer to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

jar {
    configurations.embedded.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    manifest {
        attributes([
            "Specification-Title": "opencomputers",
            "Specification-Vendor": "li.cil.oc",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${project.simpleVersion}",
            "Implementation-Vendor": "${config.mod.group}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "FMLCorePlugin": "li.cil.oc.common.launch.TransformerLoader",
            "FMLCorePluginContainsFMLMod": "true"
        ])
    }
}

javadoc {
    include 'li/cil/oc/api/**'
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    classifier = 'api'
    include 'li/cil/oc/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives apiJar
    archives javadocJar
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url System.getenv("MAVEN_PATH")
        }
    }
}

curseforge {
    apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""
    project {
        id = config.curse.project.id
        releaseType = config.curse.project.releaseType
        changelogType = "markdown"
        changelog = file("changelog.md")
        addGameVersion config.minecraft.version
        addGameVersion "Java 8"
    }
}
